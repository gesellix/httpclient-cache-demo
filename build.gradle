subprojects {
  apply plugin: 'java'

  group = 'de.gesellix'
  version = 'NONE'
  sourceCompatibility = 1.8

  repositories {
    mavenCentral()
  }
}

ext {
  testprojects = []
}

project(':httpclient-cache-4-3') {
  testprojects << it
  dependencies {
    compile("org.apache.httpcomponents:httpclient-cache:4.3")
  }
}

project(':httpclient-cache-4-4') {
  testprojects << it
  dependencies {
    compile("org.apache.httpcomponents:httpclient-cache:4.4")
  }
}

project(':httpclient-cache-4-5') {
  testprojects << it
  dependencies {
    compile("org.apache.httpcomponents:httpclient-cache:4.5")
  }
}

configure(testprojects) { p ->
  dependencies {
    compile project(':common')
  }
  task performDemo(type: JavaExec) {
    def clientSourceSets = p.sourceSets
    classpath = clientSourceSets.main.runtimeClasspath + clientSourceSets.test.runtimeClasspath
    main = 'de.gesellix.httpclientcachedemo.Demo'
  }
//  check.dependsOn performDemo
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.7'
  distributionType = Wrapper.DistributionType.ALL
}
